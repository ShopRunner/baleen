plugins {
    id "org.jetbrains.dokka" version "0.9.17" apply false
    id 'org.jetbrains.kotlin.jvm' version '1.3.11' apply false
    id 'org.jmailen.kotlinter' version '1.20.1' apply false
}

apply plugin: 'idea'

subprojects {
    apply plugin: 'org.jetbrains.dokka'
    apply plugin: 'maven-publish'
    apply plugin: 'kotlin'
    apply plugin: 'org.jmailen.kotlinter'
    apply plugin: 'signing'

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib"
        testImplementation 'org.assertj:assertj-core:3.12.2'
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.4.2'
        testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
    }

    test {
        useJUnitPlatform()
    }

    task sourceJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: dokka) {
        classifier = 'javadoc'
        from dokka
    }

    artifacts {
        archives jar
        archives sourceJar
        archives javadocJar
    }

    signing {
        sign publishing.publications
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                customizePom(pom)

                from components.java

                artifact sourceJar
                artifact javadocJar
            }
        }
        repositories {
            maven {
                url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
                credentials {
                    username sonatypeUsername
                    password sonatypePassword
                }
            }
        }
    }

    model {
        tasks.generatePomFileForMavenJavaPublication {
            destination = file("$buildDir/generated-pom.xml")
        }
    }
}


def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        // eliminate test-scoped dependencies (no need in maven central POMs)
        root.dependencies.removeAll { dep ->
            dep.scope == "test"
        }

        // add all items necessary for maven central publication
        root.children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST

            description 'Library for Validating Legacy Data'
            name 'Baleen'
            url 'https://github.com/ShopRunner/baleen'
            organization {
                name 'com.shoprunner'
                url 'https://github.com/ShopRunner'
            }
            issueManagement {
                system 'GitHub'
                url 'https://github.com/ShopRunner/baleen/issues'
            }
            licenses {
                license {
                    name 'BSD 3-Clause "New" or "Revised" License'
                    url 'https://github.com/ShopRunner/baleen/blob/master/LICENSE.txt'
                    distribution 'repo'
                }
            }
            scm {
                url 'https://github.com/ShopRunner/baleen'
                connection 'scm:git:git://github.com/ShopRunner/baleen.git'
                developerConnection 'scm:git:ssh://git@github.com:ShopRunner/baleen.git'
            }
            developers {
                developer {
                    name 'Shoprunner'
                }
            }
        }
    }
}